//===== eAthena Script =======================================
//= eAthena Jobchanger AKA Job Master
//===== By: ==================================================
//= eAthena Dev Team [Ori:LunatikBunnie] [Cur:Lance]
//===== Current Version: =====================================
//= 1.5a
//===== Compatible With: =====================================
//= eAthena SVN Trunk 6674
//===== Description: =========================================
//= Changes your job without asking too much
//= For other info, please contact me at Lunatikbunnie@gmail.com 
//= Editted menu to avoid button mashing.
//= 1.3 Added TK/SL/SG, thanks to Haplo. Fixed minor bugs [Lupus]
//=    script leaves grabage variable: 'lastJob'
//= 1.4 Added NJ/GS, changed all job numbers to Job_* constants. 
//=    All credits go to pxxx [Skotlex]
//= 1.5 Rewrite everything from scratch. [Lance]
//= 1.5a fixed Baby -> Super Baby change. Switched to JobName() [Lupus]
//============================================================
// Custom - Added ability to change to Job_Baby --Freelancer
// Custom - Added 3rd class support -- nevelis
// Custom - Added OnNPCKillEvent down... -- Skorm
//============================================================
prontera,170,180,6	script	Job Master	123,{
set skpoints, SkillPoint;
Start:
	mes "^ff0000[Job Master]^000000";
	if(Upper == 1 && Class >= Job_Rune_Knight) goto L_noReq;
	if(SkillPoint != 0){
		mes "I'm sorry, please use up all your skill points before changing jobs";
		mes "Please come again soon!";
		close;
	}
	if(JobLevel < 10) goto L_LvError;
	switch(Class){
		case Job_Novice_High:
		case Job_Baby:
		case Job_Novice:
			skill 142,1,0;
			skill 143,1,0;
			mes "Welcome, please select the job you wish to change into";
			if(lastJob != 0 && Class == Job_Novice_High){
				switch(lastJob){
					case Job_Knight:
					case Job_Crusader:
						set @target_job, Job_Swordman_High;
						break;
					case Job_Monk:
					case Job_Priest:
						set @target_job, Job_Acolyte_High;
						break;
					case Job_Alchemist:
					case Job_Blacksmith:
						set @target_job, Job_Merchant_High;
						break;
					case Job_Rogue:
					case Job_Assassin:
						set @target_job, Job_Thief_High;
						break;
					case Job_Wizard:
					case Job_Sage:
						set @target_job, Job_Mage_High;
						break;
					case Job_Hunter:
					case Job_Bard:
					case Job_Dancer:
						set @target_job, Job_Archer_High;
						break;
				}
			} else {
				if (Class == Job_Baby) {
					set .@selection,select("Baby Swordsman","Baby Mage","Baby Archer","Baby Acolyte","Baby Merchant","Baby Thief",
					"Baby Super Novice");
				} else { set .@selection,select("Swordsman","Mage","Archer","Acolyte","Merchant","Thief",
					"Super Novice","Taekwon","Gunslinger","Ninja");
				}
				switch(.@selection){
						case 7:
							if(Class == Job_Novice_High) goto L_noReq;
							if($@JC_SupNovM > BaseLevel) goto L_BvError;
							if(Upper == 2)
								set @target_job, Job_Super_Baby;
							else
								set @target_job, Job_SuperNovice;
							break;
						case 8:
							if(Class == Job_Novice_High) goto L_noReq;
							if(Upper == 2) goto L_noReq;
							set @target_job, Job_Taekwon;
							break;
						case 9:
						case 10:
							if(Class == Job_Novice_High || Upper == 2) goto L_noReq;
							set @target_job, @menu + 15;
							break;
						default:
							set @target_job, @menu;
							if(Class == Job_Novice_High) set @target_job, @target_job + 4001;
							break;
					}
			}
			mes "Are you sure you want to change to " + JobName(@target_job) + "?";
			if(select("No","Yes") == 2){
				callfunc "Job_Change", @target_job;
				set pcChanger,0;
				atcommand "@skpoint +"+skpoints+"";
				if(SkillPoint!=skpoints){
					atcommand "@skpoint +"+skpoints+"";
				}
				if(@target_job == Job_Gunslinger || @target_job == Job_Ninja || @target_job == Job_Taekwon) {
					callfunc "F_ClearJobVar";
				} else {
					if($@JC_Plat) goto L_GivePlat;
				}
			}
			close;
			break;
		default:
			if(Class >= Job_Lord_Knight && Class <= Job_Gypsy)
			{
				if(BaseLevel < 99 || JobLevel < 100) goto L_cantCh;
				switch(Class)
				{
					case Job_Lord_Knight: set @target_job, Job_Rune_Knight_T; break;
					case Job_High_Wizard: set @target_job, Job_Warlock_T; break;
					case Job_Sniper: set @target_job, Job_Ranger_T; break;
					case Job_High_Priest: set @target_job, Job_Arch_Bishop_T; break;
					case Job_Whitesmith: set @target_job, Job_Mechanic_T; break;
					case Job_Assassin_Cross: set @target_job, Job_Guillotine_Cross_T; break;
					
					case Job_Paladin: set @target_job, Job_Royal_Guard_T; break;
					case Job_Professor: set @target_job, Job_Sorcerer_T; break;
					case Job_Clown: set @target_job, Job_Minstrel_T; break;
					case Job_Gypsy: set @target_job, Job_Wanderer_T; break;
					case Job_Champion: set @target_job, Job_Sura_T; break;
					case Job_Creator: set @target_job, Job_Genetic_T; break;
					case Job_Stalker: set @target_job, Job_Shadow_Chaser_T; break;
				}
				mes "Are you sure you want to change to " + JobName(@target_job) + "?";
				if(select("Yes:No") == 1)
				{
					callfunc "Job_Change", @target_job;
					set pcChanger,0;
					atcommand "@skpoint +"+skpoints;
					if(SkillPoint!=skpoints){
						atcommand "@skpoint +"+skpoints;
					}
				}
				close;
			}
			if(JobLevel < $@JC_MinimumJB) goto L_LvError;
			deletearray @job_opt, getarraysize(@job_opt);
			if(Class < Job_Knight || Class == Job_Taekwon || (Class > Job_Baby && Class < Job_Baby_Knight) || (Class > Job_Novice_High && Class < Job_Lord_Knight)){
				if(lastJob != 0){
					set @target_job, lastJob + 4001;
				} else {
					switch(Class){
						case Job_Swordman_High:
						case Job_Baby_Swordman:
						case Job_Swordman:
							set @job_opt[0], Job_Knight;
							set @job_opt[1], Job_Crusader;
							break;
						case Job_Mage_High:
						case Job_Baby_Mage:
						case Job_Mage:
							set @job_opt[0], Job_Wizard;
							set @job_opt[1], Job_Sage;
							break;
						case Job_Archer_High:
						case Job_Baby_Archer:
						case Job_Archer:
							set @job_opt[0], Job_Hunter;
							if(Sex == 0)
								set @job_opt[1], Job_Dancer;
							else
								set @job_opt[1], Job_Bard;
							break;
						case Job_Acolyte_High:
						case Job_Baby_Acolyte:
						case Job_Acolyte:
							set @job_opt[0], Job_Priest;
							set @job_opt[1], Job_Monk;
							break;
						case Job_Merchant_High:
						case Job_Baby_Merchant:
						case Job_Merchant:
							set @job_opt[0], Job_Blacksmith;
							set @job_opt[1], Job_Alchemist;
							break;
						case Job_Thief_High:
						case Job_Baby_Thief:
						case Job_Thief:
							set @job_opt[0], Job_Assassin;
							set @job_opt[1], Job_Rogue;
							break;						
						default:
							set @job_opt[0], Job_Star_Gladiator;
							set @job_opt[1], Job_Soul_Linker;
							break;
					}
					mes "Welcome, please select the job you wish to change into";
					set @target_job, @job_opt[select(JobName(@job_opt[0]), JobName(@job_opt[1]))-1];
					if(Class > Job_Novice_High && Class < Job_Lord_Knight) set @target_job, @target_job + 4001;
				}
				mes "Are you sure you want to change to " + JobName(@target_job) + "?";
				if(select("No","Yes")==2){
					callfunc "Job_Change", @target_job;
					set pcChanger,0;
					atcommand "@skpoint +"+skpoints;
					if(SkillPoint!=skpoints){
						atcommand "@skpoint +"+skpoints;
					}
					if(@target_job == Job_Star_Gladiator || @target_job == Job_Soul_Linker) {
						callfunc "F_ClearJobVar";
					} else {
						if($@JC_Plat) goto L_GivePlat;
					}
				}
				close;
			}
			if(checkfalcon() || checkcart() || checkriding()) goto L_remove;
			if((Class >=Job_Knight) && (Class <=Job_Crusader2)){
				mes "Which path do you choose to take?";
				next;
				switch(select("Advance to 3rd Class", "Rebirth and Transcend", "I'm not ready"))
				{
					case 1:
						if(BaseLevel < 99 || JobLevel < 50) goto L_cantCh;
						switch(Class)
						{
							case Job_Knight: set @target_job, Job_Rune_Knight; break;
							case Job_Priest: set @target_job, Job_Arch_Bishop; break;
							case Job_Wizard: set @target_job, Job_Warlock; break;
							case Job_Blacksmith: set @target_job, Job_Mechanic; break;
							case Job_Hunter: set @target_job, Job_Ranger; break;
							case Job_Assassin: set @target_job, Job_Guillotine_Cross; break;
							
							case Job_Crusader: set @target_job, Job_Royal_Guard; break;
							case Job_Sage: set @target_job, Job_Sorcerer; break;
							case Job_Bard: set @target_job, Job_Minstrel; break;
							case Job_Dancer: set @target_job, Job_Wanderer; break;
							case Job_Monk: set @target_job, Job_Sura; break;
							case Job_Alchemist: set @target_job, Job_Genetic; break;
							case Job_Rogue: set @target_job, Job_Shadow_Chaser; break;
						}
						mes "^ff0000[Job Master]^000000";
						mes "If you continue to third class now, you will not be able to learn transcendent skills. Are you sure you want to continue?";
						if(select("Yes:No") == 1)
						{
							callfunc "Job_Change", @target_job;
							set pcChanger,0;
							atcommand "@skpoint +"+skpoints;
							if(SkillPoint!=skpoints){
								atcommand "@skpoint +"+skpoints;
							}
						}
						close;
					case 2:
						if(BaseLevel < 99 || JobLevel < 50) goto L_cantCh;
						set lastJob, Class;
						if(Class == Job_Knight2){
							set lastJob, Job_Knight;
						} else {
							if(Class == Job_Crusader2){
								set lastJob, Job_Crusader;
							}
						}
						jobchange Job_Novice_High;
						resetlvl(1);
						skill 142,1,0;
						skill 143,1,0;
					case 3:
						mes "^ff0000[Job Master]^000000";
						mes "Very well, come back when you are ready.";
				}
				close;
			}
	}
	mes "I'm sorry, there are no further classes for your job.";
	close;

L_GivePlat:
	if (BaseClass==Job_SuperNovice) goto L_sSuperN;
	if (BaseClass==Job_Swordman) goto L_sSword;
	if (BaseClass==Job_Mage) goto L_sMage;
	if (BaseClass==Job_Archer) goto L_sArcher;
	if (BaseClass==Job_Acolyte) goto L_sAcolyte;
	if (BaseClass==Job_Merchant) goto L_sMerchant;
	if (BaseClass==Job_Thief) goto L_sThief;
	close;
L_sSuperN:
	skill 142,1,0;
	close;
L_sSword:
	skill 142,1,0;
	skill 144,1,0;
	skill 145,1,0;
	skill 146,1,0;
	close;
L_sMage:
	skill 142,1,0;
	skill 157,1,0;
	close;
L_sArcher:
	skill 142,1,0;
	skill 147,1,0;
	skill 148,1,0;
	close;
L_sAcolyte:
	skill 142,1,0;
	skill 156,1,0;
	close;
L_sMerchant:
	skill 142,1,0;
	skill 153,1,0;
	skill 154,1,0;
	skill 155,1,0;
	close;
L_sThief:
	skill 142,1,0;
	skill 149,1,0;
	skill 150,1,0;
	skill 151,1,0;
	skill 152,1,0;
	close;

L_cantCh:
	mes "I'm sorry, you do not meet the requirements to change";
	mes "Please come again soon!";
	close;

L_LvError:
	if (Class == Job_Novice && BaseLevel == 1 && JobLevel == 1){
		mes "Would you like to change into a baby?";
		if(select("No:Yes")==1){
			mes "Aww, that's too bad!";
			close;
		}
		callfunc "Job_Change", Job_Baby;
		set pcChanger,0;
		atcommand "@skpoint +"+skpoints;
		if(SkillPoint!=skpoints){
			atcommand "@skpoint +"+skpoints;
		}
		mes "You are now a baby.";
		close;
	} else {
	mes "I'm sorry, you do not seem to have enough Job Levels";
	mes "Please come again soon!";
	close;
	}
L_BvError:
	mes "I'm sorry, you do not seem to have enough Base Levels";
	mes "Please come again soon!";
	close;

L_noReq:
	mes "I'm sorry, you do not meet the requirements to change";
	mes "Please come again soon!";
	close;

L_remove:
	mes "Please remove your cart,falcon or peco";
	mes "Please come again soon!";
	close;
OnInit:
	// Variable Setup 
	set $@JC_MinimumJB, 40; //Minimum job level for changing between 2nd and advance Class (Default : 40)
	set $@JC_Plat, 1; //Give Platinum skills on Jobchange (Default : 1-yes)
	set $@JC_SupNovM, 45; //Base Level to change into Super Novice (Default : 45)
	end;
OnPCLoginEvent:
	if(pcChanger==1){
		getitem 715,1;
		getitem 716,1;
		getitem 717,1;
		getitem 523,1;
		atcommand "@useskill 362 4 "+strcharinfo(0)+""+@useskill$;
		goto Start;
	}
	end;
OnPCDieEvent:
	if(pcChanger==1){
		warp "SavePoint",0,0;
		goto Start;
	}
	end;
OnNPCKillEvent:
if(dont_ask_me!=1&&@dont_ask!=1){
	if(JobLevel >= 10){
		if(lastJob != 0 && Class == Job_Novice_High || Class == Job_Novice) { goto mainMenu;}
	}
	if(Class >= Job_Lord_Knight && Class <= Job_Gypsy){
		if(BaseLevel >= 99 && JobLevel >= 100) { goto mainMenu;}
	}
	if(Class < Job_Knight || Class == Job_Taekwon || (Class > Job_Baby && Class < Job_Baby_Knight) || (Class > Job_Novice_High && Class < Job_Lord_Knight)){
		if(JobLevel >= 50) { goto mainMenu;}
	}
	if((Class >=Job_Knight) && (Class <=Job_Crusader2)){
		if(BaseLevel >= 99 && JobLevel >= 50) { goto mainMenu;}
	}
	end;
mainMenu:
	getitem 715,1;
	getitem 716,1;
	getitem 717,1;
	getitem 523,1;
	atcommand "@useskill 362 4 "+strcharinfo(0)+""+@useskill$;
subMenu:
	if(select("Do you want to change your class?:No.:Yes.:Don't until I relog.:Don't ask again.")==2){
		dispbottom "Ok.";
		close;
	} else if(@menu == 3){
			if((Class >=Job_Knight) && (Class <=Job_Crusader2) && BaseLevel >= 99 && JobLevel >= 50){ atcommand "@skpoint -"+SkillPoint; set skpoints, SkillPoint; goto Start;}
		setSK:
			if(SkillPoint!=0){
				set skpoints, SkillPoint;
				if(skpoints == SkillPoint){
					atcommand "@skpoint -"+SkillPoint;
					set pcChanger,1;
					goto Start;
				} else
					goto setSK;
			} else
				set pcChanger,1;
				goto Start;
	} else if(@menu==4){
		dispbottom "Ok, I won't ask untill you logout.";
		set @dont_ask, 1;
		close;
	} else if(@menu==5){
		dispbottom "Fine then... geez you don't have to be so rude.";
		set dont_ask_me, 1;
		close;
	} else
		goto subMenu;
}
}